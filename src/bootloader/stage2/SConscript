import os

from SCons.Environment import Environment
from build_scripts.utility import GlobRecursive, FindIndex, IsFileName

Import('TARGET_ENVIRONMENT')
TARGET_ENVIRONMENT: Environment

env = TARGET_ENVIRONMENT.Clone()
env.Append(
    LINKFLAGS = [
        '-Wl,-T', env.File('linker.ld').srcnode().path,
        '-Wl,-Map=' + env.File('stage2.map').path
    ],
    CPPPATH = [ 
        env.Dir('.').srcnode(), 
        env['ROOTDIR'].Dir('src/libs').srcnode()
    ],
    ASFLAGS = [ '-I', env.Dir('.').srcnode() ]
)

sources = GlobRecursive(env, '*.c') + \
          GlobRecursive(env, '*.cpp') + \
          GlobRecursive(env, '*.asm')

headers = GlobRecursive(env, '*.h') + \
          GlobRecursive(env, '*.hpp') + \
          GlobRecursive(env, '*.inc')

objects = env.Object(sources)


Import('libcore')

static_libs = [
    libcore,
]

objects = [
    *objects,
    *static_libs,
]



stage2 = env.Program('stage2.elf', objects)
env.Depends(stage2, headers)

Export('stage2')